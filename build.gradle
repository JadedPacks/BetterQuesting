buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

new File('gradle.properties').withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraftVersion}-${config.modVersion}-JADED_PATCHED"
group = "betterquesting"
archivesBaseName = "BetterQuesting"

minecraft {
	version = "${config.minecraftVersion}-${config.forgeVersion}-${config.minecraftVersion}"
	runDir = "run"
	mappings = config.mcpMappings
	replace "@VERSION@", config.modVersion
}

repositories {
	maven {
		name "CB Repo"
		url "http://chickenbones.net/maven/"
	}
}

dependencies {
	compile "codechicken:NotEnoughItems:${config.NEIVersion}:dev"
}

def root = project.projectDir.parentFile
sourceSets {
	main {
		java {
			srcDir new File(root, "src")
		}
		resources {
			srcDir new File(root, "resources")
		}
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}